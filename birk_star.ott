embed
{{ tex-preamble
\synctex=1
\usepackage{fontspec}
\usepackage{fontawesome5}
\usepackage{cancel}
}}

% term variables, represented by x in the grammar, are to be represented using
% natural numbers in Coq
metavar termvar, x, y ::= {{ coq nat }} {{ coq-equality }}

indexvar index, i, j, k, z, s ::= {{ coq nat }}

% BEGIN: grammar definitions
grammar
 
terminals :: 'terminals_' ::=
  | \                   ::   :: lambda     {{ tex \lambda }}
  | -->                 ::   :: reduce     {{ tex \Longrightarrow }}
  | ->                  ::   :: arrow      {{ tex \rightarrow }}
  | |-                  ::   :: turnstile  {{ tex \vdash }}
  | in                  ::   :: in         {{ tex \in }}
  | lock                ::   :: lock       {{ tex \text{\faLock} }}
  | empty               ::   :: empty      {{ tex \cdot }}
  | ctx                 ::   :: ctx        {{ tex \textbf{ ctx} }}
  | pi                  ::   :: pi         {{ tex \Pi }}
  | box                 ::   :: box        {{ tex \square }}
  | nat                 ::   :: nat        {{ tex \mathbb{N} }}

n :: 'n_' ::=
{{ coq (nat) }}
  | 0                   ::   :: zero {{ coq 0 }}
  | s n                 ::   :: succ {{ tex [[n]] + 1 }}
                                     {{ coq (S [[n]]) }}

term, t :: 't_' ::=
  % constructions that are properly 'term'
  | x              ::  :: Var    {{ com variables }}
  | zero           ::  :: Zero   {{ com natural number 0 }}
  | succ t         ::  :: Succ   {{ com successor of natural number t }}
  | rec t tk tz ts ::  :: Rec    {{ com (dependent) recursor for natural 
                                    numbers }}
  | \ x : t , t'   ::  :: Lam    (+ bind x in t' +) 
                                 {{ com lambda abstraction}}
  | t t'           ::  :: App    {{ com function application }}
  | quote t        ::  :: Quote  {{ com quote term t as code }}
                                 {{ tex [ [[t]] ]_{\text{\faLock} } }}
  | splice t       ::  :: Splice {{ com splice code t as term }}
                                 {{ tex [ [[t]] ]_{\text{\faUnlock} } }}
  % type constructions
  | nat            ::  :: Nat    {{ com natural number type }}
  | pi x : t , t'  ::  :: Fun    (+ bind x in t' +)
                                 {{ com dependent function type }}
  | box t          ::  :: Box    {{ com terms of $[[t]]$ as code }}
  | univ n         ::  :: Univ   {{ com type universe }}
                                 {{ tex \mathcal{U}_{[[n]]} }}
  % sugaring and substitution
  | ( t )          :: S:: Par    {{ coq [[t]] }}
  | t1 [ t2 / x ]  :: M:: Sub    {{ coq (subst_term_in_term [[t2]] [[x]] [[t1]])}}

context, C {{ tex \Gamma }} :: 'C_' ::=
{{ coq (list (option (termvar*term))) }}
  | empty        ::  :: Emp      {{ coq C_nil }}
  | C , x : t    ::  :: ConsVar  {{ coq (cons (C_var ([[x]], [[t]])) [[C]]) }}
  | C , lock     ::  :: ConsLock {{ coq (cons C_lock [[C]]) }}
  | unlock C     :: M:: Unlock   {{ coq (C_unlock [[C]]) }} 
                                 {{ tex {[[C]]}^{ \text{\faUnlock} } }}

formula :: 'formula_' ::=
  | judgement  ::  :: judgement
  | x : t in C ::  :: in {{ coq (C_in [[x]] [[t]] [[C]]) }}
                         {{ tex \Gamma = \Gamma_1, [[x]] : [[t]], \Gamma_2 
                            \text{ and } \text{\faLock} \not\in \Gamma_2 }}

embed
{{ coq
Notation C_var := Some.
Notation C_lock := None.
Notation C_nil := (@nil (option (termvar*term))).
Definition C_in (x : termvar) (T : term) (C : context) :=
  exists C1, exists C2,
    (C = List.app C1 (List.cons (Some (x,T)) C2)) /\
    (~ exists T', In (Some (x, T')) C1) /\
    (~ In C_lock C1).
Definition is_var {A : Type} (o : option A) :=
  match o with
  | C_lock   => false
  | C_var _  => true
  end.
Definition C_unlock (C : context) := filter is_var C.
}}
% END: grammar definitions

% BEGIN: substitutions
substitutions
  single t x :: subst_term_in%_term

% END: substitutions

% BEGIN: judgement definitions - typing, reduction etc.
defns
Infer :: '' ::=

defn C ctx ::  ::ctx_form::''
{{ com $[[C]]$ is a well-formed context }} by

  --------- :: ctx_emp
  empty ctx

  % don't need C ctx as C |- t : univ n implicitly carries the fact that C is 
  % well-formed
  C |- t : univ n
  --------------- :: ctx_var
  C, x:t ctx

  C ctx
  ----------- :: ctx_lock
  C, lock ctx

defn C |- t1 : t2 ::  ::type_of_term::''
{{ com $[[t1]]$ has type $[[t2]]$ under context $[[C]]$ }} by

  C ctx
  ----------------------- :: univ_form
  C |- univ n : univ s n

  C ctx
  ----------------- :: nat_form
  C |- nat : univ n

  C |- t : univ n
  C , x : t |- t' : univ n
  --------------------------- :: pi_form
  C |- pi x : t, t' : univ n

  C, lock |- t : univ n
  --------------------- :: box_form
  C |- box t : univ n

  C |- t : univ n
  ------------------ :: univ_cumul
  C |- t : univ s n

  C ctx
  --------------- :: nat_zero
  C |- zero : nat

  C |- t : nat
  ----------------- :: nat_succ
  C |- succ t : nat

  C, x : nat |- t : univ n
  C |- tk : nat
  C |- tz : t [zero / x]
  C, x : nat, y : t |- ts : t [succ x / x]
  ---------------------------------------- :: nat_elim
  C |- rec t tk tz ts : t[t1 / x]

  C ctx
  x : t in C
  ---------- :: term_var
  C |- x : t

  C |- t : univ n
  C, x : t |- t1 : t'
  ------------------------------ :: pi_intro
  C |- \x : t, t1 : pi x : t, t'

  C, x : t |- t' : univ n 
  C |- t2 : pi x : t, t'
  C |- t1 : t
  ----------------------- :: pi_elim
  C |- t2 t1 : t'

  C, lock |- t1 : t
  --------------------- :: box_intro
  C |- quote t1 : box t

  C |- t : univ n
  unlock C |- t1 : box t
  ---------------------- :: box_elim
  C |- splice t1 : t
% END: judgement definitions